apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'kotlin'

ext {

    pluginGroupId = 'il.co.galex.tools.build'
    pluginVersion = '1.1.0'
}

repositories {
    mavenCentral()
}

dependencies {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }

    compile gradleApi()
    compile localGroovy()
    compile 'com.android.tools.build:gradle:3.0.0-alpha5'
}

compileJava {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}

buildscript {
    ext.kotlin_version = '1.1.2'

    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'com.jfrog.bintray'


task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
}

publishing {
    publications {
        PermissionsPublication(MavenPublication) {
            groupId = pluginGroupId
            artifactId = 'permissions'
            version = pluginVersion
            from components.java

            artifact sourceJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier = 'javadoc'
            }
        }
    }
}

bintray {
    user bintrayUser
    key bintrayPass
    pkg {
        repo = 'maven'
        name = 'android-permissions-gradle-plugin'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/galex/android-permissions-gradle-plugin.git'

        version {
            name = pluginVersion
            desc = 'Android Permissions Gradle Plugin'
            released = new Date()
            vcsTag = pluginVersion
            publications = ['PermissionsPublication']
        }
    }
}


compileGroovy {
    dependsOn tasks.getByPath('compileKotlin')
    classpath += files(compileKotlin.destinationDir)
}
